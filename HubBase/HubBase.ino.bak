//----------------------------------------------------------------------------------------
// YamuraLog - ESP32 Hub test
//----------------------------------------------------------------------------------------
#ifndef ARDUINO_ARCH_ESP32
  #error "Select an ESP32 board"
#endif
#define YAMURANODE_ID 0
//#define DEBUG_VERBOSE
#define INITIAL_DELAY  60000
#define LOG_START      30000
#define LOG_END        60000

//----------------------------------------------------------------------------------------
//   Include files
//----------------------------------------------------------------------------------------

#include <ACAN_ESP32.h>

//----------------------------------------------------------------------------------------
//  ESP32 Desired Bit Rate
//----------------------------------------------------------------------------------------

static const uint32_t DESIRED_BIT_RATE = 1000UL * 1000UL;

CANMessage sendFrame;
CANMessage rcvFrame;
uint32_t sendDataTime = 0;
bool logging;
//----------------------------------------------------------------------------------------
//   SETUP
//----------------------------------------------------------------------------------------
void setup () 
{
  pinMode(12, OUTPUT);
  digitalWrite(12, LOW);
  for(int idx = 0; idx < 20; idx++)
  {
      digitalWrite(12, !digitalRead(12));
      delay(100);
  }
  digitalWrite(12, HIGH);
  Serial.begin (115200);
  delay (1000);
  // Configure ESP32 CAN
  Serial.printf("\n\nYamuraLogESP32 - Hub Base\n\n");
  ACAN_ESP32_Settings settings (DESIRED_BIT_RATE);
  settings.mRequestedCANMode = ACAN_ESP32_Settings::NormalMode;
  // receive all standard IDs
  const ACAN_ESP32_Filter filter = ACAN_ESP32_Filter::acceptStandardFrames();
  // start CAN with desired filtering
  const uint32_t errorCode = ACAN_ESP32::can.begin (settings, filter);
  if (errorCode == 0) 
  {
    Serial.print("Bit Rate prescaler: ");
    Serial.println(settings.mBitRatePrescaler);
    Serial.print("Time Segment 1:     ");
    Serial.println(settings.mTimeSegment1);
    Serial.print("Time Segment 2:     ");
    Serial.println(settings.mTimeSegment2);
    Serial.print ("RJW:                ");
    Serial.println (settings.mRJW);
    Serial.print("Triple Sampling:    ");
    Serial.println(settings.mTripleSampling ? "yes" : "no");
    Serial.print("Actual bit rate:    ");
    Serial.print(settings.actualBitRate());
    Serial.println(" bit/s");
    Serial.print("Exact bit rate ?    ");
    Serial.println(settings.exactBitRate() ? "yes" : "no");
    Serial.print("Sample point:       ");
    Serial.print(settings.samplePointFromBitStart());
    Serial.println("%");
    Serial.println("Configuration OK!");
  }
  else
  {
    Serial.print ("Configuration error 0x");
    Serial.println (errorCode, HEX);
  }
  logging = false;
  while(millis() < INITIAL_DELAY)
  {
    if ((millis()  % 2000) == 0)
    {
      digitalWrite(12, HIGH);
    }
    else if ((millis()  % 1000) == 0)
    {
      digitalWrite(12, LOW);
    }
  }
}
//----------------------------------------------------------------------------------------
//   LOOP
//----------------------------------------------------------------------------------------
void loop() 
{
  if (millis() % 2000 == 0) 
  {
    digitalWrite(12, HIGH);
    if(logging == false)
    {
      SendFrame(0); // heartbeat
    }
  }
  else if (millis() % 1000 == 0) 
  {
    digitalWrite(12, LOW);
  }

  if((logging == false) && ((millis() > (LOG_START + INITIAL_DELAY)) && (millis() < (LOG_END + INITIAL_DELAY))))
  {
    logging = true;
    Serial.print("START LOGGING at ");
    Serial.println(millis());
    SendFrame(1); // start logging
  }
  if((logging == true) && ((millis() < (LOG_START + INITIAL_DELAY)) || (millis() > (LOG_END + INITIAL_DELAY))))
  {
    logging = false;
    Serial.print("STOP LOGGING at ");
    Serial.println(millis());
    SendFrame(2); // stop logging
  }
  ReceiveFrame();
  delay(1);
}
void SendFrame(int subID)
{
  sendFrame.id = YAMURANODE_ID;
  sendFrame.rtr = false;
  sendFrame.ext = false;
  sendFrame.len = 8;
  sendFrame.data32[0] = subID;
  sendFrame.data32[1] = millis();
  Serial.printf("%d\tOUT\tid 0x%03x\tbytes\t%d\tdata", millis(),
                                                       sendFrame.id,
                                                       sendFrame.len);
  for(int idx = 0; idx < 8; idx++)
  {
    Serial.printf("\t0x%02X", (sendFrame.data_s8[idx] & 0xFF));
  }

  if (!ACAN_ESP32::can.tryToSend (sendFrame)) 
  {
    Serial.printf("\tFAILED TO SEND FRAME ID 0x%X!!!", sendFrame.id);
  }
  Serial.println();      
}
void ReceiveFrame()
{
  if (ACAN_ESP32::can.receive (rcvFrame)) 
  {
    #ifdef DEBUG_VERBOSE
    Serial.printf("%d\tIN\tid 0x%03x\tbytes\t%d\tdata", millis(),
                                                         rcvFrame.id,
                                                         rcvFrame.len);
    for(int idx = 0; idx < 8; idx++)
    {
      Serial.printf("\t0x%02X", (rcvFrame.data_s8[idx] & 0xFF));
    }
    Serial.println();
    #endif
  }
}